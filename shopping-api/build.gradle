plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${dependencyManagementVersion}"
    id "com.gorylenko.gradle-git-properties" version "${gitPropertiesVersion}"
    id 'pl.allegro.tech.build.axion-release' version "${axionPluginVersion}"
    id 'java'
}

apply plugin: 'io.franzbecker.gradle-lombok'

sourceCompatibility = JavaVersion.VERSION_11

scmVersion {
    tag { prefix = project.name + '-' }
    versionIncrementer 'incrementMinor'
}
project.version = scmVersion.version

def commonJarConfigClosure = {
    archiveName = "${project.name}.jar"
}

bootJar commonJarConfigClosure

springBoot {
    buildInfo {
        properties {
            version = project.version
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":shopping-lib")

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${eurekaServerVersion}" // not resolved by dependency-management plugin
    implementation 'javax.validation:validation-api'
    implementation "org.springdoc:springdoc-openapi-ui:${springDocOpenApiVersion}"

    implementation "io.micrometer:micrometer-registry-prometheus"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    compileOnly 'org.springframework.boot:spring-boot-devtools'
}

test {
    useJUnitPlatform()
}
